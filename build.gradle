buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.13.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = JavaVersion.VERSION_1_8

group = 'com.avast.gradle'
archivesBaseName = 'gradle-docker-compose-plugin'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation 'org.yaml:snakeyaml:1.27'
    testImplementation gradleTestKit()
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

test {
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-docker-compose-plugin'
        desc = 'Simplifies usage of Docker Compose for local development and integration testing in Gradle environment.'
        userOrg = 'avast'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/avast/gradle-docker-compose-plugin.git'
        websiteUrl = 'https://github.com/avast/gradle-docker-compose-plugin'
        issueTrackerUrl = 'https://github.com/avast/gradle-docker-compose-plugin/issues'
        labels = ['docker', 'docker-compose', 'gradle']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

project.ext.set('gradle.publish.key', System.getenv('GRADLE_PORTAL_KEY'))
project.ext.set('gradle.publish.secret', System.getenv('GRADLE_PORTAL_SECRET'))
pluginBundle {
    website = 'https://github.com/avast/gradle-docker-compose-plugin'
    vcsUrl = 'https://github.com/avast/gradle-docker-compose-plugin'
    description = 'Simplifies usage of Docker Compose for integration testing in Gradle environment.'
    tags = ['docker', 'docker-compose', 'gradle']
    plugins {
        dockerComposePlugin {
            id = 'com.avast.gradle.docker-compose'
            displayName = 'Gradle Docker Compose plugin'
        }
    }
}
