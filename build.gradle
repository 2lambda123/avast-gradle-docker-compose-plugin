buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.6'
    }
}
apply plugin: 'com.jfrog.bintray'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.avast.gradle'
archivesBaseName = 'docker-compose-gradle-plugin'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.yaml:snakeyaml:1.16'
    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        // Conflicting module versions: 2.4.4 vs. 2.4.1
        exclude group: 'org.codehaus.groovy'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'docker-compose-gradle-plugin'
        desc = 'Simplifies usage of Docker Compose for local development and integration testing in Gradle environment.'
        userOrg = 'avast'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/avast/docker-compose-gradle-plugin.git'
        websiteUrl = 'https://github.com/avast/docker-compose-gradle-plugin'
        issueTrackerUrl = 'https://github.com/avast/docker-compose-gradle-plugin/issues'
        labels = ['docker', 'docker-compose', 'gradle']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

project.ext.set('gradle.publish.key', System.getenv('GRADLE_PORTAL_KEY'))
project.ext.set('gradle.publish.secret', System.getenv('GRADLE_PORTAL_SECRET'))
pluginBundle {
    website = 'https://github.com/avast/docker-compose-gradle-plugin'
    vcsUrl = 'https://github.com/avast/docker-compose-gradle-plugin'
    description = 'Simplifies usage of Docker Compose for integration testing in Gradle environment.'
    tags = ['docker', 'docker-compose', 'gradle']
    plugins {
        dockerComposePlugin {
            id = 'com.avast.gradle.docker-compose'
            displayName = 'Gradle Docker Compose plugin'
        }
    }
}
