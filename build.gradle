buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}
apply plugin: 'com.jfrog.bintray'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.avast.gradle'
archivesBaseName = 'docker-compose-gradle-plugin'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.yaml:snakeyaml:1.16'
    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        // Conflicting module versions: 2.4.4 vs. 2.4.1
        exclude group: 'org.codehaus.groovy'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'docker-compose-gradle-plugin'
        userOrg = 'avast'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/avast/docker-compose-gradle-plugin.git'
        websiteUrl = 'https://github.com/avast/docker-compose-gradle-plugin'
        issueTrackerUrl = 'https://github.com/avast/docker-compose-gradle-plugin/issues'
        labels = ['docker', 'docker-compose', 'gradle']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}
